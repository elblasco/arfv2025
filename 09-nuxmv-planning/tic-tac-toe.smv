MODULE main
VAR
  B : array 1..9 of 0..2;
  -- 0 -> empty
  -- 1 -> x
  -- 2 -> o
  -- grid:
  --             1 | 2 | 3
  --           -----------
  --            4 | 5 | 6
  --           -----------
  --            7 | 8 | 9
  --           -----------
  player : 1..2;
  move   : 0..9;
INIT
  player=1 & 
  B[1] = 0 & B[2] = 0 & B[3] = 0 &
  B[4] = 0 & B[5] = 0 & B[6] = 0 &
  B[7] = 0 & B[8] = 0 & B[9] = 0;

TRANS -- every turn the player changes
  next(player) != player;
TRANS -- the players can put marks only on empty cells
  (B[1] != 0 -> next(move) != 1) &
  (B[2] != 0 -> next(move) != 2) &
  (B[3] != 0 -> next(move) != 3) &
  (B[4] != 0 -> next(move) != 4) &
  (B[5] != 0 -> next(move) != 5) &
  (B[6] != 0 -> next(move) != 6) &
  (B[7] != 0 -> next(move) != 7) &
  (B[8] != 0 -> next(move) != 8) &
  (B[9] != 0 -> next(move) != 9);
ASSIGN -- logic for placing the mark
  next(B[1]) := move = 1 ? player : B[1];
  next(B[2]) := move = 2 ? player : B[2];
  next(B[3]) := move = 3 ? player : B[3];
  next(B[4]) := move = 4 ? player : B[4];
  next(B[5]) := move = 5 ? player : B[5];
  next(B[6]) := move = 6 ? player : B[6];
  next(B[7]) := move = 7 ? player : B[7];
  next(B[8]) := move = 8 ? player : B[8];
  next(B[9]) := move = 9 ? player : B[9];
DEFINE -- check victory or draw
  win1 := (B[1] = 1 & B[2] = 1 & B[3] = 1) | (B[4] = 1 & B[5] = 1 & B[6] = 1) | (B[7] = 1 & B[8] = 1 & B[9] = 1) | 
          (B[1] = 1 & B[4] = 1 & B[7] = 1) | (B[2] = 1 & B[5] = 1 & B[8] = 1) | (B[3] = 1 & B[6] = 1 & B[9] = 1) | 
          (B[1] = 1 & B[5] = 1 & B[9] = 1) | (B[3] = 1 & B[5] = 1 & B[7] = 1);
  win2 := (B[1] = 2 & B[2] = 2 & B[3] = 2) | (B[4] = 2 & B[5] = 2 & B[6] = 2) | (B[7] = 2 & B[8] = 2 & B[9] = 2) | 
          (B[1] = 2 & B[4] = 2 & B[7] = 2) | (B[2] = 2 & B[5] = 2 & B[8] = 2) | (B[3] = 2 & B[6] = 2 & B[9] = 2) | 
          (B[1] = 2 & B[5] = 2 & B[9] = 2) | (B[3] = 2 & B[5] = 2 & B[7] = 2);
  draw := !win1 & !win2 &
          !(B[1] = 0 | B[2] = 0 | B[3] = 0 | B[4] = 0 | B[5] = 0 | B[6] = 0 | B[7] = 0 | B[8] = 0 | B[9] = 0);
INVAR -- once the game is finished, no move is performed
  move = 0 <-> (draw | win1 | win2);
  
LTLSPEC ! F draw; -- find a game that draws
LTLSPEC ! F win1; -- find a game that leads to the victory of player 1
LTLSPEC ! F win2; -- find a game that leads to the victory of player 2
